%!TEX TS-program = xelatex
%!TEX encoding = UTF-8 Unicode

\chapter{Units of Measure}
\label{sec:units-of-measure}

\minitoc

\newpage

\section{Units of Measure}

\syntax\begin{lstlisting}
Type_Representation 
    ::= ...
      | '=' 'abstract' 'measure' 
      | '=' 'measure' ['(' Long_Id ')'] [semi] measure_def
      
measure_def 
    ::= '_' -- anonymous measure, inferrable by compiler
      | measure_simple
measure_simple
    ::= measure_sequence
      | measure_simple '*' measure_simple  -- product, e.g. "'U * 'V"
      | measure_simple '/' measure_simple  -- quotient, e.g. "'U / 'V"
      | '/' measure_simple  -- reciprocal, e.g. "/'U"
      | '1'  -- dimensionless
measure_sequence
    ::= measure_power [measure_sequence]
measure_power
    ::= measure_atom
      | measure_atom '^' integer_literal  -- power of measure, e.g. "m^2"
measure_atom
    ::= Type_Var  -- variable measure, e.g. "'U"
      | Long_Id  -- named measure, e.g. "ft"
      | '(' measure_simple ')'  -- parenthesized measure, e.g. "(N m)"
      
measure_literal
    ::= '_'  -- anonymous measure, inferrable by compiler
      | measure_literal_simple
measure_literal_simple
    ::= measure_literal_sequence  -- implicit product, e.g. "m s^-3"
      | measure_literal_simple 
        '*' measure_literal_simple  -- product, e.g. "m * s^4"
      | measure_literal_simple 
        '/' measure_literal_simple  -- quotient, e.g. "m/s^3"
      | '/' measure_literal_simple  -- reciprocal, e.g. "/s"
      | '1'  -- dimensionless
measure_literal_sequence
    ::= measure_literal_power [measure_literal_sequence]
measure_literal_power
    ::= measure_literal_atom
      | measure_literal_atom '^' integer_literal  -- power of measure, e.g. "m^2"
measure_literal_atom
    ::= Long_Id  -- named measure, e.g. "ft"
      | '(' measure_literal_simple ')'  -- parenthesized measure, e.g. "(N m)"
\end{lstlisting}

Numbers in Aml can have associated units of measure, which are typically used to indicate length, volume, mass, distance and so on. By using quantities with units, the runtime is allowed to verify that arithmetic relationships have the correct units, which helps prevent programming errors. 

